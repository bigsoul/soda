////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции общего назначения:
// - для работы со списками в формах;
// - для работы с журналом регистрации;
// - для обработки действий пользователя в процессе редактирования
//   многострочного текста, например комментария в документах;
// - прочее.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьСтрокуРеквизитов(СтрокаРеквизитов, ТекущийОбъект, РеквизитыДляВерсионирования) Экспорт
	
	МетаданныеСсылки = ТекущийОбъект.Метаданные();
	
	Для Каждого Реквизит Из МетаданныеСсылки.СтандартныеРеквизиты Цикл
		
		Если СтрНайти(РеквизитыДляВерсионирования, Реквизит.Имя+",") <> 0 Тогда
			Значение			= Значение(ТекущийОбъект[Реквизит.Имя]);
			СтрокаРеквизитов	= СтрокаРеквизитов + ?(ПустаяСтрока(СтрокаРеквизитов),"",",") + Значение;
		КонецЕсли;
				
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеСсылки.Реквизиты Цикл
		
		Если СтрНайти(РеквизитыДляВерсионирования, Реквизит.Имя) <> 0 Тогда
			Значение			= Значение(ТекущийОбъект[Реквизит.Имя]);
			СтрокаРеквизитов	= СтрокаРеквизитов + ?(ПустаяСтрока(СтрокаРеквизитов),"",",") + Значение;
		КонецЕсли;
				
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из МетаданныеСсылки.ТабличныеЧасти Цикл
		
		ТаблицаЗначенийОбъекта = ТекущийОбъект[ТабличнаяЧасть.Имя].Выгрузить();
		
		Для Каждого ДанныеСтроки Из ТаблицаЗначенийОбъекта Цикл
			
			Для Каждого РеквизитТЧ Из ТабличнаяЧасть.Реквизиты Цикл
				
				Если СтрНайти(РеквизитыДляВерсионирования, РеквизитТЧ.Имя) <> 0 Тогда
					Значение			= Значение(ДанныеСтроки[РеквизитТЧ.Имя]);
					СтрокаРеквизитов	= СтрокаРеквизитов + ?(ПустаяСтрока(СтрокаРеквизитов),"",",") + Значение;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция Значение(Значение) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка")
		Или ТипЗнч(Значение) = Тип("Число")
		Или ТипЗнч(Значение) = Тип("Булево")
		Или ТипЗнч(Значение) = Тип("Дата") Тогда
		
		Возврат Значение;
		
	Иначе
		
		Возврат СсылкаВСтруктуру(Значение);
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьХешОбъекта(Объект) Экспорт
	
	Хеш = Новый ХешированиеДанных(ХешФункция.CRC32);
	Хеш.Добавить(Объект);
	
	Возврат Хеш.ХешСумма;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СсылкаВСтруктуру(Ссылка)
	
	Если Ссылка = Неопределено Тогда
		
		Возврат Неопределено;
		
	ИначеЕсли Ссылка = Null Тогда
		
		Возврат Null;
		
	КонецЕсли;
	
	ПолноеИмя = Ссылка.Метаданные().ПолноеИмя();
	
	Позиция = Найти(ПолноеИмя, ".");
	
	Если Сред(ПолноеИмя, 1, Позиция - 1) = "Перечисление" Тогда
		
		Если ЗначениеЗаполнено(Ссылка) Тогда
			
			ИмяПеречисления = Ссылка.Метаданные().Имя;
			ИндексЗначенияПеречисления = Перечисления[ИмяПеречисления].Индекс(Ссылка);
			ИмяЗначенияПеречисления = Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;
			
		Иначе
			
			ИмяЗначенияПеречисления = Неопределено;
			
		КонецЕсли;
		
		Возврат ИмяЗначенияПеречисления;
		
	Иначе
		
		Возврат Строка(Ссылка.УникальныйИдентификатор());
		
	КонецЕсли;
		
КонецФункции

#КонецОбласти